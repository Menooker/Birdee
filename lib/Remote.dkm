public class CounterOutOfBoundException
end

public class RemoteNode
		public host as string
		public port as int
		dim socket as int
		dim closed as int
		dim connected as int
		public declare function CreateThread(del as thread_proc,param as int) as RemoteThread
		public declare sub Close()
end

declare function ConnectNode(localport as int,host as String[],port as int[],mem_host as String[],mem_port as int[]) as RemoteNode[]

shared public class RemoteThread
	public final tid as int
	dim state as int
	public function getState() as int
		return state
	end
	public function Join(timeout as int,maxinterval as int) as boolean
		dim t as int
		dim itv as int=8
		for t=0 ; state!=3 ;
			if  timeout!=-1 && t>timeout then
				return false
			end
			itv=itv*2
			if itv>maxinterval then
				itv=16
			end
			t+=itv
			Sleep(itv)
		end
		//println("@"+ t +"-"+ itv)
		return true
	end
	//public declare sub Pause()
	//public declare sub Stop()
end

shared public class ThisNodeThread: RemoteThread
	public thistid as int
	override constructor initialize(del as thread_proc,param as int)
		thistid=CreateThread(del,true,param)
		state=1
	end
end


shared public class AtomicCounter
		public declare sub Set(n as int)
		public declare function Get() as int
		public declare function Inc(inc as int) as int
		public declare function Dec(dec as int) as int
		constructor initialize(n as int)
			this.Set(n)
		end
end

shared public class RemoteSpinLock
	dim cnt as AtomicCounter
	constructor initialize()
		cnt=new AtomicCounter(0)
	end
	
	public sub leave()
		cnt.Set(0)
	end

	public function enter(timeout as int,maxinterval as int) as boolean
		dim t as int=0
		dim itv as int=8
		for  ;  ;
			if cnt.Get()==0 then
				if cnt.Inc(1)==1 then
					return true
				end
			end
			if  timeout!=-1 && t>timeout then
				return false
			end
			itv=itv*2
			if itv>maxinterval then
				itv=16
			end
			t+=itv
			Sleep(itv)
		end
		//println("@"+ t +"-"+ itv)
		return true
	end
end

shared public class RBarrier
	dim cnt as int
	constructor initialize(count as int)
		cnt=count
	end
	public declare function Enter(max_wait as int) as int
end

shared public class RAccumulator
	dim cnt as int
	dim sz as int
	public buf as double[] global
	constructor initialize(count as int,size as int,buffer as double[] global)
		cnt=count
		sz=size
		buf=buffer
	end
	public declare function Accumulate(inc as double[],max_wait as int) as int
	public declare function Accumulatef(inc as float[],max_wait as int) as int
end

shared public class RSemaphore
	dim cnt as int
	constructor initialize(count as int)
		cnt=count
	end
	public declare function Acquire(max_wait as int) as int
	public declare sub Release()
end

shared public class RemoteBarrier
	dim cnt as AtomicCounter
	dim go0 as int
	dim go1 as int
	dim turn as int
	dim max_cnt as int
	constructor initialize(count as int)
		cnt=new AtomicCounter(0)
		go0=0
		go1=0
		turn=0
		max_cnt=count
	end

	public sub printstat()
		println("stat: cnt="+cnt.Get() + " go0=" + go0 + " go1=" + go1 + " turn="+turn)	
	end

	public sub enter(timeout as int,maxinterval as int) throws CounterOutOfBoundException
		dim myturn as int=turn
		//println("to inc cnt")
		dim c as int=this.cnt.Inc(1)
		//println("cnt="+c)
		if c==max_cnt then
			turn++
			if turn==100000 then
				turn = 0
			end
			cnt.Set(0)
			if turn%2==0 then
				//if turn was originally odd
				go0=0
				go1=1
			else
				go1=0
				go0=1
			end
			return
		end
		if c>max_cnt then
			//println("OUT!")
			dim e as CounterOutOfBoundException
			throw e
		end
		dim t as int=0
		dim itv as int=8
		for  ;  ;
			if myturn%2==0 then
				if go0==1 then
					return
				end
			else
				if go1==1 then
					return
				end
			end
			if  timeout!=-1 && t>timeout then
				return
			end
			itv=itv*2
			if itv>maxinterval then
				itv=16
			end
			t+=itv
			Sleep(itv)
		end
	end
end

function StartNodesEx(localport as int,nodes as string,mem as string) as RemoteNode[]
	dim file_nodes as CSVReader=new CSVReader(nodes)
	dim file_mem as CSVReader=new CSVReader(mem)
	dim ret as int=1
	dim i as int
	dim line as string[]=new string[2]
	dim ip as string[]=new string[32]
	dim port as int[]=new int[32]
	dim v as var
	for i=0; ;i++
		ret=file_nodes.ReadLineString(line,0)
		if ret==2 then
			ip[i]=line[0]
			v=line[1]
			port[i]=v
		else
			break
		end
	end
	dim nodes_ip as string[]=new string[i]
	nodes_ip[0:i]=ip[0:i]
	dim nodes_port as int[]=new int[i]
	nodes_port[0:i]=port[0:i]
	file_nodes.Close()

	for i=0; ;i++
		ret=file_mem.ReadLineString(line,0)
		if ret==2 then
			ip[i]=line[0]
			v=line[1]
			port[i]=v
		else
			break
		end
	end
	dim mem_ip as string[]=new string[i]
	mem_ip[0:i]=ip[0:i]
	dim mem_port as int[]=new int[i]
	mem_port[0:i]=port[0:i]
	file_mem.Close()
	return ConnectNode(localport,nodes_ip,nodes_port,mem_ip,mem_port)
end

function StartNodes(localport as int) as RemoteNode[]
	return StartNodesEx(localport,"slaves.txt","memory.txt")
end
