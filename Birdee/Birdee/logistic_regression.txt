require Remote
require math

dim round as int = 25
dim verify_round as int = 1

dim cur_round as int
dim time_total as int=GetClock()
dim time_calc as int
dim hosts as string[]={"127.0.0.1","127.0.0.1"}
dim ports as int[]={13080,13090}
dim mem_hosts as string[]={"127.0.0.1"}
dim mem_ports as int[]={11211}
dim nodes as RemoteNode[]=ConnectNode(5080,hosts,ports,mem_hosts,mem_ports)

println("working")
dim threads_per_node as shared int=2
dim node_num as shared int=hosts.size()
dim thread_num as shared int=threads_per_node*node_num
dim global_round as shared int= round
dim global_verify_round as int = verify_round

dim features as shared int=100
dim step_size as shared double =0.05
dim feature_len as shared int
if (features+1) % 32 == 0 then
	feature_len = (features+1) / 32 * 32
else
	feature_len = (features+1) / 32 * 32 + 32
end

dim barrier_main as shared RBarrier=new RBarrier(thread_num+1)
dim barrier_internal as shared RBarrier=new RBarrier(thread_num)
dim global_verify_miss_count as shared AtomicCounter=new AtomicCounter(0)
dim verify_miss_count as AtomicCounter=global_verify_miss_count

dim param as shared double[] global = new double[feature_len] global
dim n2 as int=feature_len * thread_num * 100
dim global_gradient as shared double[] global = new double[feature_len * node_num] global
dim data_set1 as shared double[] global =new double[n2] global
dim data_set2 as shared double[] global =new double[n2] global

dim mlocal as double[]=new double[feature_len*100]
dim file as CSVReader=new CSVReader("C:\\Users\\Menooker\\Downloads\\ds1.100.csv")


sub put_matrix()
	dim fl as int=feature_len
	dim ds as double[] global
	if cur_round % 2==0 then
		ds=data_set1
	else
		ds=data_set2
	end
	dim i as int
	dim j as int
	dim cnt as int=thread_num * 100
	for i=0;i<cnt;i+=100
		for j=0;j<100;j++
			file.ReadLine(mlocal,j*fl)
		end
		ds[i*fl: (i+100)*fl]=mlocal[0:fl*100]
	end
end

dim time as int=GetClock()
put_matrix()
dim ttid as int=0
dim ii as int
dim jj as int
for ii=0;ii<node_num;ii++
	for jj=0;jj<threads_per_node;jj++
		nodes[ii].CreateThread(threadproc,ttid)
		ttid++
	end
end
for cur_round=0;cur_round<round;cur_round++
	put_matrix()

	barrier_main.Enter(-1)
	dim t as int
	t=GetClock()-time
	println("time1 "+ t)
	time=GetClock()
end

println("================\nLearning ok, now verify")
for ;cur_round<round+verify_round;cur_round++
	put_matrix()

	barrier_main.Enter(-1)
	t=GetClock()-time
	println("time1 "+ t)
	time=GetClock()
end

println(verify_miss_count.Get()*1.0/100/thread_num)
println("total time: "+ (GetClock()-time_total))
file.Close()
gets()

dim myparam as double[]
dim gradient as double[][]
dim volatile sync2 as int
dim myfeatures as int
dim myfeature_len as int
dim ths as int
dim mystep_size as double
dim mythreads as int

sub zero_gradient(idx as int)
	dim i as int
	for i=0;i<myfeature_len;i++
		gradient[idx][i]=0
	end
end

function threadproc(id as var) as int
unsafe
	dim tid as int=id
	dim i as int
	dim j as int
	dim k as int
	dim st as int=tid % ths
	dim mround as int
	dim node_id as int=tid/ths
	dim gr as int =global_round
	dim mydata_set as double[] =new double[myfeature_len * 100]
	mydata_set[0]=0
	gradient[0][0]=0
	dim dot as double
	dim h as double
	for mround=0;mround<round;mround++
		//phase 1: copy the parameter vector
		//println("K1")
		myparam[myfeature_len*st/ths : myfeature_len*(st+1)/ths]=param[myfeature_len*st/ths : myfeature_len*(st+1)/ths]
		sync2@+=1
		for ;;
			if sync2==ths then
				break
			end
		end
		//phase 2: copy the data set
		//println("K2")
		if mround % 2==0 then
			mydata_set[0:myfeature_len * 100]=data_set1[tid*100*myfeature_len : (tid+1)*100*myfeature_len]
		else
			mydata_set[0:myfeature_len * 100]=data_set2[tid*100*myfeature_len : (tid+1)*100*myfeature_len]
		end

		zero_gradient(st)

		//phase 3: calculate the gradient on the set of data
		//println("K3")
		dim base as int
		base=0
		for i=0;i<100;i++
			dot=0
			for j=0;j<myfeatures;j++
				dot+=param[j]*mydata_set[base+j]
			end
			h=1/(1+exp(-dot))
			dim delta as double=mydata_set[base+myfeatures]-h
			for j=0;j<myfeatures;j++
				gradient[st][j]+= mystep_size * delta * mydata_set[base+j]
			end
			base+=myfeature_len
		end

		//phase 4: sum up the local gradient
		//println("K4")
		if st==0 then
			for i=1;i<ths;i++
				for j=0;j<myfeatures;j++
					gradient[0][j]+=gradient[i][j]
				end
			end
			global_gradient[node_id * myfeature_len : (node_id+1) * myfeature_len]=gradient[0][0:myfeature_len]
		end
		sync2=0
		barrier_internal.Enter(-1)

		//phase 5: (only on thread 0), sum over the global gradient, calculate the new parameter vector
		//println("K5")
		if tid==0 then
			mydata_set[0: myfeature_len * mythreads/ths]=global_gradient[0: myfeature_len * mythreads/ths]
			for i=0;i<mythreads/ths;i++
				for j=0;j<myfeatures;j++
					myparam[j]+=mydata_set[i*myfeature_len + j]
				end
			end
			param[0: myfeature_len]=myparam[0: myfeature_len]
		end
		barrier_main.Enter(-1)
		
	end


	myparam[feature_len*st/ths : feature_len*(st+1)/ths]=param[feature_len*st/ths : feature_len*(st+1)/ths]
	sync2@+=1
	for ;;
		if sync2==ths then
			break
		end
	end

	//now verify the parameter vector
	dim total_round as int=verify_round+round
	for ;mround<total_round;mround++
		//phase 1: copy the data set
		if mround % 2==0 then
			mydata_set[0:myfeature_len * 100]=data_set1[tid*100*myfeature_len : (tid+1)*100*myfeature_len]
		else
			mydata_set[0:myfeature_len * 100]=data_set2[tid*100*myfeature_len : (tid+1)*100*myfeature_len]
		end

		//phase 2: calculate the misses on the set of data
		dim base as int
		base=0
		for i=0;i<100;i++
			dot=0
			for j=0;j<myfeatures;j++
				dot+=param[j]*mydata_set[base+j]
			end
			h=1/(1+exp(-dot))
			if h>=0.5 && mydata_set[base+myfeatures]==0.0 then
				verify_miss_count.Inc(1)
			elseif h<0.5 && mydata_set[base+myfeatures]==1.0 then
				verify_miss_count.Inc(1)
			end
			base+=myfeature_len
		end
		barrier_main.Enter(-1)
	end
end

sub RemoteInitialize()
	verify_miss_count=global_verify_miss_count
	verify_round = 1
	myfeatures = features
	myfeature_len = feature_len
	ths =threads_per_node
	mystep_size=step_size
	mythreads=thread_num
	round=global_round
	gradient=new double[ths][myfeature_len]
	myparam=new double[myfeature_len]
end
